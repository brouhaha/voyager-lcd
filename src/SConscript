# Copyright 2023 Eric Smith
# SPDX-License-Identifier: GPL-3.0-only

import SCons

Import('env')

def find_file(filename, pathnames):
    paths = [env.Dir(pathname) for pathname in pathnames]
    return SCons.Node.FS.find_file(filename, paths)



env.Append(CXXFLAGS = ['-g', '--std=c++20'])

local_tools = ['qt6', 'otf_to_c']
if env['target'] in ('win32', 'win64'):
    local_tools += ['wxs', 'msi']
for lt in local_tools:
    env.Tool(lt, toolpath = ['.'])


env.EnableQt6Modules(['QtCore',
                      'QtGui',
		      'QtWidgets'])

font = env.File('Aileron-Bold.otf')
font_c = env.OTF_TO_C(font)
font_h = env.OTF_TO_H(font)

SOURCES = [font_c,
           'main.cpp',
           'main_window.cpp',
	   'central_widget.cpp',
	   'g_hex_line_edit.cpp',
           'g_lcd.cpp',
	   'g_lcd_annunciator.cpp',
           'g_lcd_digit.cpp',
           'g_lcd_segment.cpp',
           'g_lcd_view.cpp',
	   'g_registers.cpp',
	   'g_user_code.cpp',

	   'lcd.cpp',
	   'lcd_registers.cpp',
	   'lcd_mapper.cpp',

	   'hp15c_user_code.cpp'
]


executable = env.Program('voyager-lcd', SOURCES)[0]

if env['target'] in ('win32', 'win64'):
    wxs_template = env.File('voyager-lcd.wxst')

    dll_names = ['Qt6Core',
                 'Qt6Gui',
                 'Qt6Widgets',
                 'libstdc++-6',
                 'libwinpthread-1',
                 'icui18n71',
                 'icuuc71',
                 'icudata71',
                 'libpcre2-16-0',
                 'libpcre2-8-0',
                 'libssp-0',
                 'zlib1',
                 'libfreetype-6',
                 'libharfbuzz-0',
                 'libpng16-16',
                 'libbz2-1',
                 'libglib-2.0-0',
                 'libintl-8',
                 'iconv',
                 'qwindows'
                 ]

    if env['target'] == 'win32':
        dll_names.append('libgcc_s_dw2-1')
    elif env['target'] == 'win64':
        dll_names.append('libgcc_s_seh-1')

    dlls = [find_file(dll_name + '.dll', env['DLLPATH']) for dll_name in dll_names]
    files = [executable] + dlls
    wxs = env.WXS('voyager-lcd.wxs', [wxs_template, files])
    env.MSI(wxs)
